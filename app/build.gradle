apply plugin: 'com.android.application'
apply plugin: 'bugly'
// 用于编译 kotlin
apply plugin: 'kotlin-android'
// 用于 kotlin 扩展
apply plugin: 'kotlin-android-extensions'

static def releaseTime() {
  return new Date().format("yyyy.MM.dd", TimeZone.getTimeZone("UTC"))
}
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

android {
  compileSdkVersion 26
  buildToolsVersion '26.0.3'

  defaultConfig {
    applicationId "me.venjerlu.gankio"
    minSdkVersion 21
    targetSdkVersion 27
    versionCode 5
    versionName "1.3"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    ndk {
      // 设置支持的SO库架构
      abiFilters 'armeabi', 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
    }
  }

  buildTypes {
    release {
      minifyEnabled true
      zipAlignEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      applicationVariants.all { variant ->
        variant.outputs.all { output ->
          def outputFile = output.outputFile
          if (outputFile != null && outputFile.name.endsWith('.apk')) {
            def fileName = "app-${defaultConfig.versionName}-${releaseTime()}.apk"
            outputFileName = fileName
          }
        }
      }
    }
  }

  tasks.withType(JavaCompile) {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
  }

  lintOptions {
    disable 'MissingTranslation', 'ExtraTranslation'
    disable "InvalidPackage"
    lintConfig file("lint.xml")
    abortOnError false
  }

  packagingOptions {
    exclude 'META-INF/DEPENDENCIES.txt'
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/NOTICE.txt'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/notice.txt'
    exclude 'META-INF/license.txt'
    exclude 'META-INF/dependencies.txt'
    exclude 'META-INF/LGPL2.1'
    exclude 'META-INF/rxjava.properties'
  }

  sourceSets {
    main.jniLibs.srcDirs = ['libs']
  }

  bugly {
    appId = properties.getProperty("bugly.appId")
    appKey = properties.getProperty("bugly.appKey")
  }

  sourceSets {
    //    main.java.srcDirs += 'src/main/kotlin'
  }
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
    exclude group: 'com.android.support', module: 'support-annotations'
  })
  implementation 'com.android.support:appcompat-v7:27.0.0'
  implementation 'com.android.support:support-v4:27.0.0'
  implementation 'com.android.support:cardview-v7:27.0.0'
  implementation 'com.android.support:recyclerview-v7:27.0.0'
  implementation 'com.android.support:design:27.0.0'
  testImplementation 'junit:junit:4.12'

  // Util
  implementation 'com.elvishew:xlog:1.2.1'
  implementation 'com.blankj:utilcode:1.3.6'


  // ImageLoader
  implementation 'com.github.bumptech.glide:glide:3.7.0'
  implementation 'com.github.bumptech.glide:okhttp3-integration:1.4.0@aar'
  implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.7.2'
  implementation 'jp.wasabeef:glide-transformations:2.0.2'
  // 人像居中
  implementation 'com.github.aryarohit07:glide-facedetection-transformation:0.3.0'

  // Reactive Extensions
  implementation 'io.reactivex.rxjava2:rxjava:2.1.2'
  implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'

  // Network
  implementation 'com.squareup.retrofit2:retrofit:2.3.0'
  implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
  implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
  implementation 'com.squareup.okhttp3:okhttp:3.9.0'
  implementation 'com.squareup.okio:okio:1.13.0'
  implementation 'com.squareup.okhttp3:logging-interceptor:3.6.0'

  // UI
  implementation 'me.yokeyword:fragmentation:0.10.1'
  implementation 'com.appeaser.sublimepickerlibrary:sublimepickerlibrary:2.1.1' //日历
  implementation 'com.github.mancj:MaterialSearchBar:0.6' //搜索框

  //  compile 'me.yokeyword:fragmentation-swipeback:0.7.9'
  //  compile 'com.afollestad.material-dialogs:core:0.9.0.1'
  //  compile 'me.drakeet.support:about:1.0.2'
  //  compile 'me.drakeet.multitype:multitype:2.4.1'

  // Dependency Injection
  implementation 'com.google.dagger:dagger:2.9'
  annotationProcessor 'com.google.dagger:dagger-compiler:2.9'
  implementation 'com.jakewharton:butterknife:8.8.1'
  annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

  // Canary
  debugImplementation 'com.github.moduth:blockcanary-android:1.2.1'
  releaseImplementation 'com.github.moduth:blockcanary-no-op:1.2.1'
  debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
  releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
  implementation 'com.facebook.stetho:stetho:1.5.0'
  implementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'

  //bugly
  implementation 'com.tencent.bugly:crashreport:2.6.6.1'
  implementation 'com.tencent.bugly:nativecrashreport:3.3.1'

  // kotlin 基于 jre8
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
}
